/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */

options { static = true; }

PARSER_BEGIN(Lexer)

import javax.swing.JFileChooser;
import java.io.File;
import java.io.FileInputStream;
import java.util.Scanner;

public class Lexer {
	public static void execute(Lexer analisador) throws ParseException {
		analisador.main();
		System.out.println("\n --------------------");
	    System.out.println(" Mas ah indio veio!");
	    System.out.println(" --------------------");
	}
  
	public static void main(String[] args) {
        try {
            if(args.length == 0) {
    		    System.out.println("\nInforme seu codigo: ");
    		    execute(new Lexer(System.in));
    		} else if(args[0].contains("-f")) {
    		    System.out.println("\nSelecione o arquivo do programa: ");
    		 	JFileChooser arquivo = new JFileChooser();
	          	arquivo.showOpenDialog(null);
	          	File selected = arquivo.getSelectedFile();
	          	if(selected != null) {
	          	 	FileInputStream stream = new FileInputStream(selected.getAbsolutePath());
	          	 	execute(new Lexer(stream));
	         	} else {
	         	  System.out.println("\n\n------------------------------------");
	         	  System.out.println("Processo cancelado pelo usuario!");
	         	  System.out.println("--------------------------------");
	         	  return;
	        	}
    		} else {
    		  	FileInputStream stream = new FileInputStream(args[0]);
	          	execute(new Lexer(stream));
    		}
        } catch (Throwable e) {
            System.out.println("Programa invalido!\n " + e.getMessage());
        }
    }
}

PARSER_END(Lexer)

SKIP :
{
  " "
| "\r"
| "\t"
}

/* PARA IGNORAR OS COMENTARIOS */
MORE: { "//" : IN_SINGLE_LINE_COMMENT | "/*" : IN_MULTI_LINE_COMMENT }
<IN_SINGLE_LINE_COMMENT> SPECIAL_TOKEN: { <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT }
<IN_MULTI_LINE_COMMENT> SPECIAL_TOKEN: { <MULTI_LINE_COMMENT: "*/" >: DEFAULT }
<IN_SINGLE_LINE_COMMENT,IN_MULTI_LINE_COMMENT> MORE : { < ~[] > }

TOKEN: /* OPERADORES */
{ 
  < MAIS: "+" >
| < MENOS: "-" >
| < VEZES: "*" >
| < DIVIDIDO: "/" >
| < ELEVADO: "^" >
| < ATRIBUICAO: "=" >
| < IGUAL: "==" >
| < DIFERENTE: "!=" >
| < MAIOR: ">" >
| < MENOR: "< " >
| < MAIOR_IGUAL: ">=" >
| < MENOR_IGUAL: "<=" >
| < OU: "ou" >
| < E: "e" >
}

TOKEN: /* SIMBOLOS RESERVADAS */
{
  < FIM_PROGRAMA: "pareHome" >
| < FIM_LINHA: "\n" >
| < VIRGULA: "," >
| < INICIO_BLOCO: "tche" >
| < FIM_BLOCO: "bah" >
| < INICIO_OPERACAO_LINHA: "dai" >
| < FIM_OPERACAO_LINHA: "ne" >
| < DEFINICAO_DE_FUNCAO: "funcao" >
| < ABRE_PARENTESE: "(" >
| < FECHA_PARENTESE: ")" >
| < LOQUEANDO: "loqueando" > // WHILE
| < REPETECO: "repeteco" > // FOR
| < MIN_REPETECO: "de" >
| < MAX_REPETECO: "ate" >
| < SE: "se" >
| < SENAO: "senao" >
}

TOKEN: /* TIPOS DE DADOS */
{
  < REDONDO: "redondo" > // INTEIRO
| < QUEBRADO: "quebrado" > // FLOAT
| < TROVA: "trova" > // STRING
| < LOGICO: "logico" > // BOOL
| < VAZIO: "vazio" > // VOID
}

TOKEN: /* FORMALIZACAO DE DADOS */
{
  < BOOLEANO: "causo" | "anedota" > // VERDADEIRO E FALSO
| < NOME_VARIAVEL: (<MINUSCULAS> | "_") (<LETRA> | <DIGITO> | "_")* >
| < NOME_FUNCAO: <MAIUSCULAS> (<LETRA> | <DIGITO> | "_")* >
| < #LETRA: [ "A" - "Z", "a" - "z" ] > // "PRIVADO"
| < #MINUSCULAS: [ "a" - "z" ] >  // "PRIVADO"
| < #MAIUSCULAS: [ "A" - "Z" ] >  // "PRIVADO"
| < NUMERO: (< DIGITO >)+("."(< DIGITO >)+)? >
| < #DIGITO: [ "0" - "9" ] >  // "PRIVADO"
| < TEXTO: ("\"" ((~["\"","\\","\n","\r"]) | < ASCII >)* "\"") | ("'" ((~["'","\\","\n","\r"]) | < ASCII >)* "'") > 
| < #ASCII: ("\\" ( ["n","t","b","r","f","\\","'","\""] | ["0"-"7"] ( ["0"-"7"] )? | ["0"-"3"] ["0"-"7"] ["0"-"7"])) >  // "PRIVADO"
}

void main():
{}
{
  (linha() | bloco())*
  fimDePrograma()
}

/* METODOS RELACIONADOS AOS TOKENS PARA POSTERIOR CONSTRUCAO DA ARVORE DE DERIVACAO */
void nomeVariavel():
{}
{ < NOME_VARIAVEL > }

void numeroComSinal():
{}
{ ((< MAIS > | < MENOS >)?< NUMERO >) }

void simboloAtribuicao():
{}
{ < ATRIBUICAO > }

void fimLinha():
{}
{ < FIM_LINHA > }

void virgula():
{}
{ < VIRGULA > }

void loqueando():
{}
{ < LOQUEANDO > }

void repeteco():
{}
{ < REPETECO > }

void inicioBloco():
{}
{ < INICIO_BLOCO > }

void fimBloco():
{}
{ < FIM_BLOCO > <FIM_LINHA > }

void inicioOperacaoLinha():
{}
{ < INICIO_OPERACAO_LINHA > }

void fimOperacaoLinha():
{}
{ < FIM_OPERACAO_LINHA > }

void minRepeteco():
{}
{ < MIN_REPETECO > }

void maxRepeteco():
{}
{ < MAX_REPETECO > }

void se():
{}
{ < SE > }

void senao():
{}
{ < SENAO > }


/* INICIO DA LOGICA DE VALIDACAO DA LINGUAGEM */
void linha():
{}
{ (elementoDeLinha() fimLinha()) | fimLinha() }

void elementoDeLinha():
{}
{ atribuicao() | declaracaoDeVariavel() }

void atribuicao():
{}
{ nomeVariavel() lacoDeAtribuicao() }

void lacoDeAtribuicao():
{}
{ simboloAtribuicao() expressaoMatematica()  }

void expressaoMatematica():
{}
{ expressaoLogica() (operadorMatematico() expressaoLogica())*  }

void expressaoLogica():
{}
{ atribuivel() (operadorLogico() atribuivel())*  }

void atribuivel():
{}
{ expressaoEntreParenteses() | numeroComSinal() | nomeVariavel() | < TEXTO > | < BOOLEANO > }

void expressaoEntreParenteses():
{}
{ (< ABRE_PARENTESE > expressaoMatematica() < FECHA_PARENTESE >) }

void operadorMatematico():
{}
{ < MAIS > | < MENOS > | < VEZES > | < DIVIDIDO > | < ELEVADO > }

void operadorLogico():
{}
{ < MAIOR > | < MENOR > | < MAIOR_IGUAL > | < MENOR_IGUAL > | < IGUAL > | < DIFERENTE > | < OU > | < E > }

void declaracaoDeVariavel():
{}
{  tipoDeDado() listaDeDeclaracao() }

void tipoDeDado():
{}
{ < REDONDO > | < QUEBRADO > | < TROVA > | < LOGICO > }

void listaDeDeclaracao():
{}
{ nomeVariavel() (lacoDeAtribuicao())? (virgula() nomeVariavel() (lacoDeAtribuicao())?)* }

void bloco():
{}
{ inicioLoqueando() | inicioRepeteco() | inicioSe() }

void inicioLoqueando():
{}
{ loqueando() expressaoMatematica() meioLoqueando() }

void meioLoqueando():
{}
{ inicioOperacaoLinha() linha() | inicioBloco() fimLinha() blocoLoqueando() }

void blocoLoqueando():
{}
{ linha() blocoLoqueando() | bloco() blocoLoqueando() | fimBloco() }

void inicioRepeteco():
{}
{ repeteco() nomeVariavel() minRepeteco() expressaoMatematica() maxRepeteco() expressaoMatematica() meioRepeteco() }

void meioRepeteco():
{}
{ inicioOperacaoLinha() linha() | inicioBloco() fimLinha() blocoRepeteco() }

void blocoRepeteco():
{}
{ linha() blocoRepeteco() | bloco() blocoRepeteco() | fimBloco() }

void inicioSe():
{}
{ se() expressaoMatematica() meioSe() }

void meioSe():
{}
{ inicioOperacaoLinha() linha() (LOOKAHEAD(1) inicioSenao())? | inicioBloco() fimLinha() blocoSe() }

void blocoSe():
{}
{ linha() blocoSe() | bloco() blocoSe() | fimBloco() | inicioSenao() }

void inicioSenao():
{}
{ senao() (inicioSe() | inicioOperacaoLinha() linha() | inicioBloco() fimLinha() fimBlocoSenao()) }

void fimBlocoSenao():
{}
{ linha() fimBlocoSenao() | bloco() fimBlocoSenao() | fimBloco()  }

void fimDePrograma():
{}
{ < EOF > | < FIM_PROGRAMA > }

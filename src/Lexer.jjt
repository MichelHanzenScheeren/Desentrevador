/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */

options { static = true; }

PARSER_BEGIN(Lexer)

import javax.swing.JFileChooser;
import java.io.File;
import java.io.FileInputStream;
import java.util.Scanner;

public class Lexer {
	public static void execute(Lexer analisador) throws ParseException {
		SimpleNode node = analisador.main();
		// node.dump("->");
		System.out.println("\n  ------------------------------------------");
	    System.out.println("   Mas ah indio veio, que escrita bagual!");
	    System.out.println("  ------------------------------------------\n");
	}

	public static void extensionCheck(String file) throws Throwable {
	  	String extension = "";
		int i = file.lastIndexOf('.');
		if (i >= 0) { extension = file.substring(i+1); }

		if(!extension.equals("entrevero")) {
			throw new Throwable("Meu favor, nao sabe nem a extensao, bixo buro! Mah use um aquivo .entrevero.");
		}
	}
  
	public static void main(String[] args) {
        try {
            if(args.length == 0) {
    		    System.out.println("\nBota teu codigo ai guri: ");
    		    execute(new Lexer(System.in));
    		} else if(args[0].contains("-f")) {
    		    System.out.println("\nTu trata de pega o arquivo ai entao: ");
    		 	JFileChooser arquivo = new JFileChooser();
	          	arquivo.showOpenDialog(null);
	          	File selected = arquivo.getSelectedFile();
	          	if(selected != null) {
	          	  	extensionCheck(selected.getAbsolutePath());
	          	 	FileInputStream stream = new FileInputStream(selected.getAbsolutePath());
	          	 	execute(new Lexer(stream));
	         	} else {
	         	  System.out.println("\n\n------------------------------------");
	         	  System.out.println("O tongo veio, cancelou o negocio!");
	         	  System.out.println("--------------------------------");
	         	  return;
	        	}
    		} else {
    		  	extensionCheck(args[0]);
    		  	FileInputStream stream = new FileInputStream(args[0]);
	          	execute(new Lexer(stream));
    		}
        } catch (Throwable e) {
            System.out.println("  Deu ruim pia!\n    " + e.getMessage() + "\n");
        }
    }
}

PARSER_END(Lexer)

SKIP :
{
  " "
| "\r"
| "\t"
}

/* PARA IGNORAR OS COMENTARIOS */
MORE: {"/*" : IN_MULTI_LINE_COMMENT }
<IN_MULTI_LINE_COMMENT> SPECIAL_TOKEN: { <MULTI_LINE_COMMENT: "*/" >: DEFAULT }
<IN_MULTI_LINE_COMMENT> MORE : { < ~[] > }

TOKEN: /* OPERADORES */
{ 
  < MAIS: "+" >
| < MENOS: "-" >
| < VEZES: "*" >
| < DIVIDIDO: "/" >
| < ELEVADO: "^" >
| < RESTO: "%" >
| < ATRIBUICAO: "=" >
| < IGUAL: "==" >
| < DIFERENTE: "!=" >
| < MAIOR: ">" >
| < MENOR: "<" >
| < MAIOR_IGUAL: ">=" >
| < MENOR_IGUAL: "<=" >
| < OU: "ou" >
| < E: "e" >
| < NEGACAO: "!" >
}

TOKEN: /* PALAVRAS RESERVADAS */
{
  < PARE_HOME: "pareHome" >
| < TCHE: "tche" > // ABERTURA DE BLOCO
| < BAH: "bah" > // FECHAMENTO DE BLOCO
| < DAI: "dai" > // OPERACAO DE LINHA
| < FUNCAO: "funcao" >
| < DEVOLTA: "devolta" >
| < LOQUEANDO: "loqueando" > // WHILE
| < REPETECO: "repeteco" > // FOR
| < DE: "de" >
| < ATE: "ate" >
| < SE: "se" >
| < SENAO: "senao" >
| < TROVAR: "Trovar"> // METODO DE ESCRITA
| < ESCUITAR: "Escuitar" > // METODO DE LEITURA
}

TOKEN: /* SIMBOLOS */
{
  < FIM_LINHA: "\n" >
| < VIRGULA: "," >
| < ABRE_PARENTESE: "(" >
| < FECHA_PARENTESE: ")" >
}

TOKEN: /* TIPOS DE DADOS */
{
  < REDONDO: "redondo" > // INTEIRO
| < QUEBRADO: "quebrado" > // FLOAT
| < TROVA: "trova" > // STRING
| < LOGICO: "logico" > // BOOL
| < VAZIO: "vazio" > // VOID
}

TOKEN: /* IDENTIFICADORES */
{
  < BOOLEANO: "causo" | "anedota" > // VERDADEIRO E FALSO
| < NOME_VARIAVEL: (<MINUSCULAS> | "_") (<LETRA> | <DIGITO> | "_")* >
| < NOME_FUNCAO: <MAIUSCULAS> (<LETRA> | <DIGITO> | "_")* >
| < #LETRA: [ "A" - "Z", "a" - "z" ] >
| < #MINUSCULAS: [ "a" - "z" ] > 
| < #MAIUSCULAS: [ "A" - "Z" ] >
| < NUMERO: (< DIGITO >)+("."(< DIGITO >)+)? >
| < #DIGITO: [ "0" - "9" ] >
| < TEXTO: ("\"" (~["\n", "\r", "\""])* "\"") | ("'" (~["\n", "\r", "'"])* "'") >
}

/* INICIO DO CODIGO RELACIONADO A ANALISE SINTATICA */
SimpleNode main():
{}
{
  (linha() | bloco())*
  (pareHome() | fimDeArquivo())
  { return jjtThis; }
}


/* OPERADORES */
void mais():
{}
{ < MAIS > }

void menos():
{}
{ < MENOS > }

void vezes():
{}
{ < VEZES > }

void dividido():
{}
{ < DIVIDIDO > }

void elevado():
{}
{ < ELEVADO > }

void resto():
{}
{ < RESTO > }

void maior():
{}
{ < MAIOR > }

void menor():
{}
{ < MENOR > }

void maiorIgual():
{}
{ < MAIOR_IGUAL > }

void menorIgual():
{}
{ < MENOR_IGUAL > }

void igual():
{}
{ < IGUAL > }

void diferente():
{}
{ < DIFERENTE > }

void ou():
{}
{ < OU > }

void e():
{}
{ < E > }

void negacao():
{}
{ < NEGACAO > }


/* PALAVRAS RESERVADAS */
void loqueando():
{}
{ < LOQUEANDO > }

void repeteco():
{}
{ < REPETECO > }

void tche():
{}
{ < TCHE > }

void bah():
{}
{ < BAH > <FIM_LINHA > }

void dai():
{}
{ < DAI > }

void de():
{}
{ < DE > }

void ate():
{}
{ < ATE > }

void se():
{}
{ < SE > }

void senao():
{}
{ < SENAO > }

void funcao():
{}
{ < FUNCAO > }

void devolta():
{}
{ < DEVOLTA > }

void trovar():
{}
{ < TROVAR > }

void escuitar():
{}
{ < ESCUITAR > }

void fimDeArquivo():
{}
{ < EOF > }

void pareHome():
{}
{ < PARE_HOME > }


/* SIMBOLOS */
void simboloAtribuicao():
{}
{ < ATRIBUICAO > }

void abreParentese():
{}
{ < ABRE_PARENTESE > }

void fechaParentese():
{}
{ < FECHA_PARENTESE > }

void fimLinha():
{}
{ < FIM_LINHA > }

void virgula():
{}
{ < VIRGULA > }


/* TIPOS DE DADO */
void redondo():
{}
{ < REDONDO > }

void quebrado():
{}
{ < QUEBRADO > }

void trova():
{}
{ < TROVA > }

void logico():
{}
{ < LOGICO > }

void vazio():
{}
{ < VAZIO > }


/* IDENTIFICADORES: */
void nomeVariavel():
{}
{ < NOME_VARIAVEL > }

void nomeFuncao():
{}
{ < NOME_FUNCAO > }

void texto():
{}
{ < TEXTO > }

void numeroComSinal():
{}
{ ((mais() | menos())?< NUMERO >) }

void booleano():
{}
{ < BOOLEANO > }


/* REGRAS SINTATICAS */
void linha():
{}
{ (elementoDeLinha())? fimLinha() }

void elementoDeLinha():
{}
{ atribuicao() | declaracaoDeVariavel() | chamadaDeFuncao() | funcoesNativas() }

void bloco():
{}
{ inicioDoLoqueando() | inicioDoRepeteco() | inicioSe() | definicaoDeFuncao() }

// LINHA
void atribuicao():
{}
{ nomeVariavel() (operadorMatematico())? lacoDeAtribuicao() }

void lacoDeAtribuicao():
{}
{ simboloAtribuicao() expressao()  }

void expressao():
{}
{ (negacao())? atribuivel() (operador() expressao())? }

void atribuivel():
{}
{ abreParentese() expressao() fechaParentese() | numeroComSinal() | texto() | booleano() | nomeVariavel() | chamadaDeFuncao() | funcaoEscuitar() }

void operador():
{}
{ operadorMatematico() | operadorLogico() | conectorLogico() }

void operadorMatematico():
{}
{ mais() | menos() | vezes() | dividido() | elevado() | resto() }

void operadorLogico():
{}
{ maior() | menor() | maiorIgual() | menorIgual() | igual() | diferente() }

void conectorLogico():
{}
{ e() | ou() }

void expressaoMatematica():
{}
{ atribuivelMatematico() lacoMatematico() }

void lacoMatematico():
{}
{ (operadorMatematico() expressaoMatematica()) | {} }

void atribuivelMatematico():
{}
{ (abreParentese() expressaoMatematica() fechaParentese()) | numeroComSinal() | texto() | nomeVariavel() | chamadaDeFuncao() | funcaoEscuitar() }

void expressaoLogica():
{}
{ (negacao())? (expressaoLogicaEntreParenteses() | (booleano() lacoLogico()) | verificacaoDeNome() | (primitivosLogicos() lacoMatematico() ligacaoLogica())) }

void expressaoLogicaEntreParenteses():
{}
{ abreParentese() expressaoLogica() fechaParentese() lacoLogico() }

void lacoLogico():
{}
{ conectorLogico() expressaoLogica() | {} }

void verificacaoDeNome():
{}
{ (nomeVariavel() | chamadaDeFuncao()) opcoesDeNome() }

void opcoesDeNome():
{}
{ (operadorMatematico() expressaoMatematica() ligacaoLogica()) | ligacaoLogica() | lacoLogico() }

void ligacaoLogica():
{}
{ operadorLogico() expressaoMatematica() lacoLogico() }

void primitivosLogicos():
{}
{ numeroComSinal() | texto() | funcaoEscuitar() }

void declaracaoDeVariavel():
{}
{ tipoDeDado() listaDeDeclaracao() }

void tipoDeDado():
{}
{ redondo() | quebrado() | trova() | logico() }

void listaDeDeclaracao():
{}
{ nomeVariavel() (lacoDeAtribuicao())? (virgula() listaDeDeclaracao())? }

void chamadaDeFuncao():
{}
{ nomeFuncao() abreParentese() (passagemDeParametros())? fechaParentese()  }

void passagemDeParametros():
{}
{ expressao() (virgula() passagemDeParametros())? }

void funcoesNativas():
{}
{ funcaoTrovar() |  funcaoEscuitar() }

void funcaoTrovar():
{}
{ trovar() abreParentese() expressao() fechaParentese() }

void funcaoEscuitar():
{}
{ escuitar() abreParentese() (texto())? fechaParentese()  }

// BLOCO
void inicioDoLoqueando():
{}
{ loqueando() expressaoLogica() meioBlocoRepeticao() }

void inicioDoRepeteco():
{}
{ repeteco() nomeVariavel() de() expressaoMatematica() ate() expressaoMatematica() meioBlocoRepeticao() }

void meioBlocoRepeticao():
{}
{ dai() linha() | tche() fimLinha() fimBlocoRepeticao() }

void fimBlocoRepeticao():
{}
{ linha() fimBlocoRepeticao() | bloco() fimBlocoRepeticao() | bah() }

void inicioSe():
{}
{ se() expressaoLogica() (seDeUmaLinha() | (tche() fimLinha() blocoSe())) }

void seDeUmaLinha():
{}
{ dai() elementoDeLinha() (fimLinha() | senao() (dai())? linha()) }

void blocoSe():
{}
{ linha() blocoSe() | bloco() blocoSe() | bah() | inicioSenao() }

void inicioSenao():
{}
{ senao() (inicioSe() | tche() fimLinha() fimBlocoSenao()) }

void fimBlocoSenao():
{}
{ linha() fimBlocoSenao() | bloco() fimBlocoSenao() | bah()  }

void definicaoDeFuncao():
{}
{ funcao() tipoDeRetorno() nomeFuncao() abreParentese() (definicaoDeParametros())? fechaParentese() meioDeFuncao() }

void tipoDeRetorno():
{}
{ vazio() | tipoDeDado() }

void definicaoDeParametros():
{}
{ tipoDeDado() nomeVariavel() (lacoDeAtribuicao())? (virgula() definicaoDeParametros())? }

void meioDeFuncao():
{}
{ dai() funcaoDeLinha() | tche() fimLinha() blocoDeFuncao() }

void funcaoDeLinha():
{}
{ linha() | devolta() expressao() fimLinha() }

void blocoDeFuncao():
{}
{ linha() blocoDeFuncao() | bloco() blocoDeFuncao() | retornoDeFuncao() | bah() }

void retornoDeFuncao():
{}
{ devolta() expressao() fimLinha() bah() }

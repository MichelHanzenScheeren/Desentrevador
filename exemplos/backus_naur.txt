/* TOKENS */
<booleano> ::= "causo" | "anedota"
<nomeVariavel> ::= (<minusculas> | "_") (<letra> | <digito> | "_")*
<nomeFuncao> ::= <maiusculas> (<letra> | <digito> | "_")*
<letra> ::= <minusculas> | <maiusculas>
<minusculas> ::= ["a" - "z"]
<maiusculas> ::= ["A" - "Z"]
<numero> ::= (<digito>)+("."(<digito>)+)?
<numeroComSinal> ::= ("+" | "-")? <numero>
<digito> ::= ["0" - "9"]
<texto> ::= ("\"" (~["\n", "\r", "\""])* "\"") | ("'" (~["\n", "\r", "'"])* "'")


/* REGRAS SINTATICAS */
<S> ::= (<linha> | <bloco>)* ("pareHome" | "EOF")
<linha> ::= (<elementoDeLinha>)? "\n"  
<elementoDeLinha> ::= <atribuicao> | <declaracaoDeVariavel> | <chamadaDeFuncao> | <funcoesNativas> | <retornoDeFuncao>
<bloco> ::= <inicioDoLoqueando> | <inicioDoRepeteco> | <inicioSe> | <definicaoDeFuncao>

// LINHA
<atribuicao> ::= <nomeVariavel> (<operadorMatematico>)? <lacoDeAtribuicao>
<lacoDeAtribuicao> ::= "=" <expressao>
<expressao> ::= ("!")? <atribuivel> (<operador><expressao>)?
<atribuivel> ::= "("<expressao>")" | <numeroComSinal> | <texto> | <booleano> | <nomeVariavel> 
                | <chamadaDeFuncao> | <funcaoEscuitar>
<operador> ::= <operadorMatematico> | <operadorLogico> | <conectorLogico>
<operadorMatematico> ::= "+" | "-" | "*" | "/" | "^" | "%"
<operadorLogico> ::= ">" | "<" | ">=" | "<=" | "==" | "!=" 
<conectorLogico> ::= "e" | "ou"

<declaracaoDeVariavel> ::=  <tipoDeDado><listaDeDeclaracao>
<tipoDeDado> ::= "redondo" | "quebrado" | "trova" | "logico"
<listaDeDeclaracao> ::= <nomeVariavel> (<lacoDeAtribuicao>)?  (","<listaDeDeclaracao>)?

<chamadaDeFuncao> ::= <nomeFuncao> "(" (<passagemDeParametros>)? ")"
<passagemDeParametros> ::= <expressao> (","<passagemDeParametros>)?

<funcoesNativas> ::= <funcaoTrovar> | <funcaoEscuitar>
<funcaoTrovar> ::= "Trovar" "(" <expressao> ")"
<funcaoEscuitar> ::= "Escuitar" "(" (<texto>)? ")"

<retornoDeFuncao> ::= "devolta" <expressao>

// BLOCO
<inicioDoLoqueando> ::= "loqueando"<expressao><meioBlocoRepeticao>
<inicioDoRepeteco> ::= "repeteco"<nomeVariavel>"de"<expressao>"ate"<expressao><meioBlocoRepeticao>
<meioBlocoRepeticao> ::= "dai"<linha> | "tche""\n"<fimBlocoRepeticao>
<fimBlocoRepeticao> ::= <linha><fimBlocoRepeticao> | <bloco><fimBlocoRepeticao> | "bah""\n"

<inicioSe> ::= "se"<expressao> (<seDeUmaLinha> | "tche""\n"<blocoSe>)
<seDeUmaLinha> ::= "dai"<elementoDeLinha> ("\n" | "senao" ("dai")? <linha>)
<blocoSe> ::= <linha><blocoSe> | <bloco><blocoSe> | "bah""\n" | <inicioSenao>
<inicioSenao> ::= "senao" (<inicioSe> | ("tche""\n"<fimBlocoSenao>))
<fimBlocoSenao> ::= <linha><fimBlocoSenao> | <bloco><fimBlocoSenao> | "bah""\n"

<definicaoDeFuncao> ::= "funcao" <tipoDeRetorno> <nomeFuncao> "(" (<definicaoParametros>)? ")" <meioDeFuncao>
<tipoDeRetorno> ::= "vazio" | <tipoDeDado>
<definicaoParametros> ::= <tipoDeDado> <nomeVariavel> (<lacoDeAtribuicao>)? ("," <definicaoDeParametros>)?
<meioDeFuncao> ::= "dai"<linha> | "tche""\n"<blocoFuncao>
<blocoFuncao> ::= <linha><blocoFuncao> | <bloco><blocoFuncao> | "bah""\n"
